IMPLEMENTATION MODULE FIL;

FROM Storage IMPORT ALLOCATE, DEALLOCATE;
IMPORT ARG,BasicIO,CV,ErrLists,Idents,IO,O,OB,OT,Scanner,STR,Strings,Strings1,SYSTEM,SysLib,Tree,UTI;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE FileModificationTime(fn:ARRAY OF CHAR) : SysLib.timeT;
VAR buf:SysLib.Stat;
BEGIN (* FileModificationTime *)
 IF UTI.stat(SYSTEM.ADR(fn),buf)=0 THEN 
    RETURN buf.stMtime; 
 ELSE 
    RETURN MIN(INTEGER); 
 END; (* IF *)
END FileModificationTime;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE AdjustClientSourceTime(time : SysLib.timeT); 
BEGIN (* AdjustClientSourceTime *)
 IF (ActP^.PrevP#NIL) & (time>ActP^.PrevP^.SourceTime) THEN 
    ActP^.PrevP^.SourceTime:=time; 
 END; (* IF *)
END AdjustClientSourceTime;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE ScanModulename(fn:ARRAY OF CHAR):STR.tStr;
VAR src,dst,high:INTEGER; c:CHAR; mn:ARRAY [0..Strings.cMaxStrLength] OF CHAR;
BEGIN (* ScanModulename *)
 src:=Strings1.Length(fn)-5; dst:=0; high:=HIGH(mn);

 LOOP
  IF (src<0) OR (dst>=high) THEN EXIT; END; (* IF *)
  CASE CAP(fn[src]) OF
  |'A'..'Z','0'..'9': mn[dst]:=fn[src]; DEC(src); INC(dst);
  ELSE                EXIT;
  END; (* CASE *)
 END; (* LOOP *)

 mn[dst]:=0C; DEC(dst); src:=0;
 WHILE src<dst DO
  c:=mn[src]; mn[src]:=mn[dst]; mn[dst]:=c;
  INC(src); DEC(dst);
 END; (* WHILE *)
 
 RETURN STR.Alloc(mn); 
END ScanModulename;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE IsOpen(moduleIdent:Idents.tIdent) : BOOLEAN;
VAR e:tElemPtr;
BEGIN (* IsOpen *)
 e:=ActP;
 WHILE e#NIL DO
  IF e^.ModuleIdent=moduleIdent THEN RETURN TRUE; END; (* IF *)
  e:=e^.PrevP;
 END; (* WHILE *)

 RETURN FALSE;
END IsOpen;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE Open(Filename:ARRAY OF CHAR) : BOOLEAN;
VAR i,p:INTEGER; dir,dn,fn,on:ARRAY [0..Strings.cMaxStrLength] OF CHAR; e:tElemPtr;
BEGIN (* Open *)
 IF Filename[0]=0C THEN 
    IF ActP#NIL THEN RETURN FALSE; END; (* IF *)

    INC(NestingDepth);
    NEW(ActP);
    WITH ActP^ DO
     SourceDir    := STR.Alloc(''); 
     Filename     := STR.Alloc('<stdinput>');
     Modulename   := STR.Alloc(''); 
     ModuleIdent  := Idents.NoIdent;
     SourceTime   := 0; 
     IsForeign    := FALSE; 
     Library      := OT.NoString; 
     ObjectTime   := 0; 
     TreeRoot     := NIL;
     MainTable    := NIL;
     ModuleEntry  := NIL; 
     ConstTable   := NIL; 
     NextLocLabel := 0; 
     ErrorList    := ErrLists.New();
     ProcCount    := 0; 
     nofSpills    := 0; 
     nofLRs       := 0; 
     PrevP        := NIL;
    END; (* WITH *)
    RETURN TRUE;
 END; (* IF *)

 Tree.NewTree;
 STR.Copy(fn,Filename);
 p:=Strings1.pos(Extension,fn);
 IF (p=HIGH(fn)+1) OR (fn[p+4]#0C) THEN STR.Append(fn,Extension); END; (* IF *)

 FOR i:=0 TO ARG.ImportDirC-1 DO
  STR.Copy(dir,ARG.ImportDirP^[i]^); 
  IF (dir[0]#0C) & (dir[STR.Length(dir)]#'/') THEN STR.Append(dir,'/'); END; (* IF *)
  STR.Concat(dn,dir,fn); 

  IF BasicIO.Accessible(dn,FALSE) THEN 
     Scanner.BeginFile(dn);
         
     INC(NestingDepth);
     NEW(e);
     e^.SourceDir    := STR.Alloc(dir); 
     e^.Filename     := STR.Alloc(fn); 
     e^.Modulename   := ScanModulename(fn);
     e^.ModuleIdent  := UTI.IdentOf(e^.Modulename^);
     e^.SourceTime   := FileModificationTime(dn); 
     e^.IsForeign    := FALSE; 
     e^.Library      := OT.NoString; 
     STR.Conc3(on,dir,e^.Modulename^,'.o'); 
     e^.ObjectTime   := FileModificationTime(on); 
     e^.TreeRoot     := NIL;
     e^.MainTable    := NIL;
     e^.ModuleEntry  := NIL; 
     e^.ConstTable   := NIL; 
     e^.NextLocLabel := 0; 
     e^.ErrorList    := ErrLists.New();
     e^.ProcCount    := 0; 
     e^.nofSpills    := 0; 
     e^.nofLRs       := 0; 
     e^.PrevP        := ActP;
     ActP:=e; CV.Init;
    
     RETURN TRUE;
  END; (* IF BasicIO.Accessible(fn,FALSE) *)
 END; (* FOR i *)
 
 RETURN FALSE; 
END Open;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE WriteErrorFile;
VAR f:IO.tFile; fn:ARRAY [0..200] OF CHAR; acc:BOOLEAN;
BEGIN (* WriteErrorFile *)
 IF ActP^.ModuleIdent=Idents.NoIdent THEN RETURN; END;

 STR.Conc3(fn,ActP^.SourceDir^,ActP^.Filename^,ErrorExtension);
 acc:=BasicIO.Accessible(fn,TRUE);

 IF ErrLists.Length(ActP^.ErrorList)=0 THEN 
    IF acc THEN BasicIO.Erase(fn,acc); END;
 ELSE 
    f:=IO.WriteOpen(fn);
    ErrLists.Write(f,ActP^.ErrorList,'');
    IO.WriteClose(f);

    IF ~ARG.OptionEagerErrorMsgs THEN 
       STR.Conc3(fn,ActP^.SourceDir^,ActP^.Filename^,': ');
       ErrLists.Write(2,ActP^.ErrorList,fn);
    END; (* IF *)
 END; (* IF *)
END WriteErrorFile;

(*------------------------------------------------------------------------------------------------------------------------------*)
PROCEDURE Close;
VAR e:tElemPtr;
BEGIN (* Close *)
 IF ActP=NIL THEN RETURN; END;

 WriteErrorFile;

 STR.Free(ActP^.SourceDir); 
 STR.Free(ActP^.Filename); 
 STR.Free(ActP^.Modulename); 
 ErrLists.Kill(ActP^.ErrorList);
 Tree.OldTree;

 e:=ActP; ActP:=ActP^.PrevP;
 DISPOSE(e);
 DEC(NestingDepth);
END Close;

(*------------------------------------------------------------------------------------------------------------------------------*)
BEGIN (* FIL *)
 ActP         := NIL;
 NestingDepth := 0;
END FIL.

