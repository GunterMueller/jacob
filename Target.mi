IMPLEMENTATION MODULE Target;
IMPORT ADR,ARG,ASM,BasicIO,CV,FIL,LAB,O,OT,TextIO,STR,SysLib,SYSTEM;
FROM Storage IMPORT ALLOCATE,DEALLOCATE;
FROM ASM IMPORT
   al,ah,bl,bh,cl,ch,dl,dh,ax,bx,cx,dx,si,di,eax,ebx,ecx,edx,esi,edi,ebp,esp,
   S,i,x,iL,ioL,R,oLBIf,oLBI,oLB,oLIf,oL,o,oBIf,oBI,oB,oIf,LBIf,LBI,LB,LIf,L,BIf,BI,B,If,
   b,w,l;
FROM ASMOP IMPORT   
   add,and,call,cld,imul,jmp,lea,leave,mov,movs,popl,pushl,repz,ret,shl,shr,sub;

CONST MaxNameLength = 300;
TYPE  tLinkElemP    = POINTER TO tLinkElem;
      tLinkElem     = RECORD
                       dir  ,
                       name : STR.tStr;
                       next : tLinkElemP;
                      END;
      tLibElemP     = POINTER TO tLibElem;
      tLibElem      = RECORD
                       name : STR.tStr;
                       next : tLibElemP;
                      END;
VAR   linkHd,linkTl:tLinkElemP; libHd,libTl:tLibElemP;
      fOut:TextIO.File; curDir,curModulename:ARRAY [0..MaxNameLength] OF CHAR; 

(************************************************************************************************************************)
PROCEDURE BeginningOfFile(dir,name:ARRAY OF CHAR);
VAR fn:ARRAY [0..MaxNameLength] OF CHAR; 
BEGIN (* BeginningOfFile *)
 STR.Copy(curDir,dir); STR.Copy(curModulename,name); 
 
 STR.Conc3(fn,dir,name,".s"); 
 TextIO.OpenOutput(fOut,fn); 
 ASM.Begin(fOut); 
END BeginningOfFile;

(************************************************************************************************************************)
PROCEDURE EndOfFile;
BEGIN (* EndOfFile *)
 ASM.End;
 TextIO.PutLn(fOut); 
 TextIO.Close(fOut); 
END EndOfFile;

(************************************************************************************************************************)
PROCEDURE Module(dir,name:ARRAY OF CHAR; globalspace:LONGINT); 
BEGIN (* Module *)  
 BeginningOfFile(dir,name); 

 IF globalspace>=0 THEN 
    ASM.Comm(FIL.ActP^.ModuleEntry^.ModuleEntry.globalLabel,8+globalspace);
 END; (* IF *)
END Module;
             
(************************************************************************************************************************)
PROCEDURE BeginModule(tempSpace:LONGINT; fTempLabel:LAB.T);
VAR name,nLab,dLab,iLab:LAB.T; s:ARRAY [0..200] OF CHAR; 
BEGIN (* BeginModule *)
 name:=STR.Alloc(curModulename); 
 nLab:=LAB.AppS(name,'$I$N'); 
 dLab:=LAB.AppS(name,'$I$D'); 
 iLab:=LAB.AppS(name,'$I'); 

 ASM.Text;
 ASM.Label(nLab); 

 STR.Concat(s,curModulename,'$I'); 
 ASM.Asciz(s);

 ASM.Align(2);
 ASM.LongL(nLab); 
 ASM.LongI(0); 
 ASM.LongL(LAB.NILPROC); 

 ASM.Label(dLab); 
 ASM.LongI(-1);
 ASM.Ln;				             
 					             
 ASM.Align(2);				             
 ASM.Globl(iLab); 			             
 ASM.Label(iLab); 			             
 ASM.C1 ( pushl  ,  R(ebp)                           );
 ASM.CS2( mov,l  ,  R(esp),R(ebp)                    );
 ASM.C1 ( pushl  ,  iL(dLab)                         );
 ASM.CS2( sub,l  ,  ioL(tempSpace,fTempLabel),R(esp) ); 
 ASM.Ln;
END BeginModule;

(************************************************************************************************************************)
PROCEDURE EndModule(fTempLabel:LAB.T; fTempSize:LONGINT);
BEGIN (* EndModule *)
 ASM.CS2( mov,l  ,  R(ebp),R(esp) ); 
 ASM.C1 ( popl   ,  R(ebp)        );
 ASM.C0 ( ret                     ); 
 ASM.LabelDef(fTempLabel,fTempSize); 

 CV.Code;
 EndOfFile;
END EndModule;

(************************************************************************************************************************)
PROCEDURE Assemble():BOOLEAN; 
VAR s:ARRAY [0..2*MaxNameLength] OF CHAR; ok,f:BOOLEAN; 
BEGIN (* Assemble *)
 STR.Concat(s,ARG.AsmScript^," ");
 IF ARG.OptionElf THEN STR.Append(s,"-elf "); END;
 STR.App2(s,curDir,curModulename);
 ok:=(SysLib.system(SYSTEM.ADR(s))=0); 

 IF ~ARG.OptionKeepTemporaries THEN 
    STR.Conc3(s,curDir,curModulename,".s");
    BasicIO.Erase(s,f); 
 END; (* IF *)
 
 RETURN ok; 
END Assemble;

(************************************************************************************************************************)
PROCEDURE ClearLinklist;
VAR ek,dk:tLinkElemP; eb,db:tLibElemP;
BEGIN (* ClearLinklist *)
 ek:=linkHd; 
 WHILE ek#NIL DO
  dk:=ek; ek:=ek^.next; STR.Free(dk^.name); DISPOSE(dk); 
 END; (* WHILE *)             
 linkHd:=NIL; linkTl:=NIL; 

 eb:=libHd; 
 WHILE eb#NIL DO
  db:=eb; eb:=eb^.next; STR.Free(db^.name); DISPOSE(db); 
 END; (* WHILE *)             
 libHd:=NIL; libTl:=NIL; 
END ClearLinklist;

(************************************************************************************************************************)
PROCEDURE AddToLinklist(dir,name:ARRAY OF CHAR);
VAR e:tLinkElemP; 
BEGIN (* AddObjectToLinklist *)
 NEW(e); e^.dir:=STR.Alloc(dir); e^.name:=STR.Alloc(name); e^.next:=NIL; 

 IF linkHd=NIL THEN linkHd:=e; ELSE linkTl^.next:=e; END; (* IF *)
 linkTl:=e; 
END AddToLinklist;

(************************************************************************************************************************)
PROCEDURE AddToLibraryList(name:OT.oSTRING); 
VAR s:ARRAY [0..300] OF CHAR; e:tLibElemP; 
BEGIN (* AddToLibraryList *)
 OT.oSTRING2ARR(name,s); 
 IF s[0]=0C THEN RETURN; END;
 
 NEW(e); e^.name:=STR.Alloc(s); e^.next:=NIL; 

 IF libHd=NIL THEN libHd:=e; ELSE libTl^.next:=e; END; (* IF *)
 libTl:=e; 
END AddToLibraryList;

(************************************************************************************************************************)
PROCEDURE GenerateRootModule(dir,mainname:ARRAY OF CHAR):BOOLEAN;
VAR dLab,nLab,iLab:LAB.T; e:tLinkElemP;
BEGIN (* GenerateRootModule *)
 BeginningOfFile(dir,mainname); 
 
 (*** Type descriptor of the root module ***)

 IF ARG.OptionCommentsInAsm THEN 
    ASM.SepLine; ASM.CmtLnS('TDesc of root init proc'); 
 END; (* IF *)

 dLab:=STR.Alloc('_$I$D'); 
 nLab:=STR.Alloc('_$I$N'); 
 iLab:=STR.Alloc('_$I'); 

 ASM.Globl(dLab); 
 ASM.Text;
 ASM.Label(nLab); 
 ASM.Asciz("_$I"); 
 ASM.Ln;

 ASM.Align(2);
 ASM.LongL(nLab); 
 ASM.LongI(0); 
 ASM.LongL(LAB.NILPROC); 

 ASM.Label(dLab); 
 ASM.LongI(-1);
 ASM.Ln;

 (*** Prolog of the init proc of the root module ***)

 IF ARG.OptionCommentsInAsm THEN ASM.SepLine; ASM.CmtLnS('root init proc'); END; (* IF *)

 ASM.Globl(iLab); 
 ASM.Align(2);
 ASM.Label(iLab); 
 ASM.C1 ( pushl  ,  R(ebp)        ); 
 ASM.C1 ( pushl  ,  i(0)          ); 
 ASM.CS2( mov,l  ,  R(esp),R(ebp) );
 ASM.C1 ( pushl  ,  iL(dLab)      ); 
 ASM.Ln;

 (*** Initialization of the type descriptors for the globals of all modules ***)

 IF ARG.OptionCommentsInAsm THEN ASM.CmtLnS('	Initialize the global TDescs of all modules'); END; (* IF *)

 e:=linkHd; 
 WHILE e#NIL DO
  ASM.CS2( mov,l  ,  iL(STR.AppS(e^.name,'$D')),L(STR.AppS(e^.name,'$G')) ); 
  e:=e^.next; 
 END; (* WHILE *)
 ASM.Ln;

 (*** Linkage of the global sections of all modules ***)

 IF ARG.OptionCommentsInAsm THEN ASM.CmtLnS('	Link the globals of all modules'); END; (* IF *)

 e:=linkHd; 
 ASM.CS2( mov,l  ,  R(ebp),oL(4,STR.AppS(e^.name,'$G')) ); 
 WHILE e^.next#NIL DO
  ASM.CS2( mov,l  ,  ioL(4,STR.AppS(e^.name,'$G')),oL(4,STR.AppS(e^.next^.name,'$G')) ); 
  e:=e^.next; 
 END; (* WHILE *)
 ASM.CS2( lea,l  ,  oL(4,STR.AppS(e^.name,'$G')),R(ebp) ); 
 ASM.Ln;

 (*** Invocation of the init procs of all modules ***)

 IF ARG.OptionCommentsInAsm THEN ASM.CmtLnS('	Call the module init procs'); END; (* IF *)

 e:=linkHd; 
 WHILE e#NIL DO
  ASM.C1( call  ,  L(STR.AppS(e^.name,'$I')) ); 
  e:=e^.next; 
 END; (* WHILE *)
 ASM.Ln;
 
 (*** Epilog of the init proc of the root module ***)

 ASM.CS2( add,l  ,  i(8),R(esp) ); 
 ASM.C1 ( popl   ,  R(ebp)      );
 ASM.C0 ( ret                   );
 ASM.Ln;

 (*** FirstModuleTDesc ***)

 IF ARG.OptionCommentsInAsm THEN ASM.SepLine; END; (* IF *)

 ASM.GLabel(LAB.FirstModuleTDesc); 
 ASM.LongL(STR.AppS(linkTl^.name,'$D')); 
 
 EndOfFile;

 RETURN Assemble();
END GenerateRootModule;

(************************************************************************************************************************)
PROCEDURE Link(dir,mainname:ARRAY OF CHAR):BOOLEAN; 
VAR ek:tLinkElemP; eb:tLibElemP; dirLen,cmdLen:CARDINAL; cmd:POINTER TO ARRAY [0..100000] OF CHAR; 
    rootname:ARRAY [0..MaxNameLength] OF CHAR; OK,f:BOOLEAN; 
BEGIN (* Link *)          
 IF linkHd=NIL THEN RETURN FALSE; END; (* IF *)
 
 STR.Concat(rootname,'_',mainname); 
 IF ~GenerateRootModule(dir,rootname) THEN RETURN FALSE; END; (* IF *)

 (*** Calculation of the length of the link script call ***)
 (* e.g. "oc.linker dir/main dir/_main.o "                          *)

 dirLen:=STR.Length(dir);               
 cmdLen:=STR.Length(ARG.LinkScript^)+1   
        +5 (* [-elf] *)
        +dirLen+STR.Length(mainname)+1      
        +dirLen+STR.Length(rootname)+3; 
 
 ek:=linkHd; 
 WHILE ek#NIL DO
  INC(cmdLen,STR.Length(ek^.dir^)+STR.Length(ek^.name^)+3); 
  ek:=ek^.next; 
 END; (* WHILE *)
 eb:=libHd; 
 WHILE eb#NIL DO
  INC(cmdLen,STR.Length(eb^.name^)+1); 
  eb:=eb^.next; 
 END; (* WHILE *)

 INC(cmdLen,2); 
 ALLOCATE(cmd,cmdLen); 
 STR.Concat(cmd^,ARG.LinkScript^," "); 
 IF ARG.OptionElf THEN STR.Append(cmd^,"-elf "); END;
 STR.App5(cmd^,mainname," ",dir,rootname,".o "); 
 ek:=linkHd; 
 WHILE ek#NIL DO STR.App3(cmd^,ek^.dir^,ek^.name^,".o "); ek:=ek^.next; END;
 eb:=libHd; 
 WHILE eb#NIL DO STR.App2(cmd^,eb^.name^," "); eb:=eb^.next; END;

 OK:=(SysLib.system(cmd)=0); 

 IF ~ARG.OptionKeepTemporaries THEN 
    STR.Conc3(cmd^,dir,rootname,".o"); 
    BasicIO.Erase(cmd^,f); 
 END; (* IF *)

 DEALLOCATE(cmd,cmdLen); 
 RETURN OK; 
END Link;

(************************************************************************************************************************)
BEGIN (* Target *)
 fOut:=0; linkHd:=NIL; linkTl:=NIL; 
END Target.
